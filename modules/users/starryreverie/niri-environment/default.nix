{
  config,
  lib,
  pkgs,
  flakeRoot,
  ...
}:
let
  cfg = config.users.users.starryreverie.maid;
in
{
  # Requires the corresponding system module
  imports = [ (flakeRoot + /modules/system/niri-environment) ];

  users.users.starryreverie.maid = {
    packages = with pkgs; [
      # Supporting utilities
      xwayland-satellite
      brightnessctl
      libnotify
      playerctl

      # System
      dconf-editor

      # Documents
      newsflash
      papers
      textpieces

      # Pictures
      curtail
      loupe
      switcheroo

      # Media
      amberol
      eartag
      mousai
      showtime

      # Efficiency
      eyedropper
      gnome-calendar
      gnome-clocks
    ];

    file.xdg_config."niri/config.kdl".text = lib.mkAfter (builtins.readFile ./config.kdl);

    systemd.services.kanshi = {
      serviceConfig.ExecStart = "${lib.getExe pkgs.kanshi}";
      wantedBy = [ "niri-session.target" ];
      partOf = [ "niri-session.target" ];
      after = [ "niri-session.target" ];
    };

    systemd.services.wpaperd = {
      serviceConfig.ExecStart = "${lib.getExe' pkgs.wpaperd "wpaperd"}";
      wantedBy = [ "niri-session.target" ];
      partOf = [ "niri-session.target" ];
      after = [
        "niri-session.target"
        "xdg-desktop-portal.service"
        cfg.systemd.services.kanshi.name
      ];
    };

    systemd.services.swaync = {
      serviceConfig.ExecStart = "${lib.getExe pkgs.swaynotificationcenter}";
      wantedBy = [ "niri-session.target" ];
      partOf = [ "niri-session.target" ];
      after = [ "niri-session.target" ];
    };

    systemd.services.clipboard = {
      script = "${lib.getExe' pkgs.wl-clipboard "wl-paste"} --watch ${lib.getExe pkgs.cliphist} store";
      wantedBy = [ "niri-session.target" ];
      partOf = [ "niri-session.target" ];
      after = [ "niri-session.target" ];
    };

    systemd.services.waybar = {
      serviceConfig.ExecStart = "${lib.getExe pkgs.waybar}";
      path =
        (with pkgs; [
          swaynotificationcenter
          hyprlock
          rofi
          wpaperd
        ])
        ++ (lib.optionals config.services.pipewire.wireplumber.enable [
          pkgs.wireplumber
        ])
        ++ (lib.optionals config.hardware.bluetooth.enable [
          pkgs.blueman
        ]);
      wantedBy = [ "niri-session.target" ];
      partOf = [ "niri-session.target" ];
      after = [ "niri-session.target" ];
    };

    systemd.services.swayidle = {
      serviceConfig.ExecStart =
        let
          lockCommand = "${lib.getExe pkgs.hyprlock}";
          lockGracefullyCommand = "${lib.getExe pkgs.hyprlock} --grace 5";
          monitorsOffCommand = "${lib.getExe pkgs.niri} msg action power-off-monitors";
          monitorsOnCommand = "${lib.getExe pkgs.niri} msg action power-on-monitors";
          suspendCommand = "${lib.getExe' pkgs.systemd "systemctl"} suspend";
        in
        builtins.concatStringsSep " " [
          "${lib.getExe pkgs.swayidle}"
          "timeout 180 ${lib.escapeShellArg lockGracefullyCommand}"
          "timeout 185 ${lib.escapeShellArg monitorsOffCommand}"
          "timeout 300 ${lib.escapeShellArg suspendCommand}"
          "resume ${lib.escapeShellArg monitorsOnCommand}"
          "before-sleep ${lib.escapeShellArg "${monitorsOffCommand}; ${lockCommand}"}"
          "after-resume ${lib.escapeShellArg monitorsOnCommand}"
          "lock ${lib.escapeShellArg "${monitorsOffCommand}; ${lockCommand}"}"
          "unlock ${lib.escapeShellArg monitorsOnCommand}"
        ];
      wantedBy = [ "niri-session.target" ];
      partOf = [ "niri-session.target" ];
      after = [ "niri-session.target" ];
    };

    systemd.services.sway-audio-idle-inhibit = {
      serviceConfig.ExecStart = "${lib.getExe pkgs.sway-audio-idle-inhibit}";
      serviceConfig.Restart = "on-failure";
      wantedBy = [ "niri-session.target" ];
      partOf = [ "niri-session.target" ];
      after = [
        "niri-session.target"
        cfg.systemd.services.swayidle.name
      ];
    };

    systemd.services.polkit-authentication-agent = {
      serviceConfig.ExecStart = "${pkgs.polkit_gnome}/libexec/polkit-gnome-authentication-agent-1";
      wantedBy = [ "niri-session.target" ];
      partOf = [ "niri-session.target" ];
      after = [
        "niri-session.target"
        "xdg-desktop-portal.service"
      ];
    };
  };
}
